buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
    project {
        languageLevel = '1.8' //due to a bug in gradle or IDEA, this doesn't work :(
    }
}

version = "1.12-3.0.0-alpha.6"
group = "de.sanandrew.mods"
archivesBaseName = "ClaySoldiersMod"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12-14.21.1.2387"
    runDir = "run"
    mappings = "snapshot_20170716"
}

repositories {
    maven {
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12:4.7.1.69:api"
    runtime "mezz.jei:jei_1.12:4.7.1.69"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

task dlSanLib(type: LibDownloader) {
    sourceUrl = 'https://minecraft.curseforge.com/projects/sanlib/files/2501131/download'
    target = file('libs/SanLib.jar')
}

setupDecompWorkspace.dependsOn dlSanLib
setupDevWorkspace.dependsOn dlSanLib
setupCiWorkspace.dependsOn dlSanLib

jar {
    manifest {
        attributes 'FMLAT': 'claysoldiers_at.cfg'
    }
}

sourceJar {
    from sourceSets.main.java
    appendix = 'src'
    archiveName = "${baseName}-${version}-${appendix}.jar"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
    archiveName = "${baseName}-${version}-${appendix}.jar"
    manifest {
        attributes 'FMLAT': 'claysoldiers_at.cfg'
    }
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
}

class LibDownloader extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
